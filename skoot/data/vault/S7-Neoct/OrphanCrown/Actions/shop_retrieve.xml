<object clone="/usr/SkotOS/obj/meracha" owner="fyth">
  <SkotOS:MerAcHa action_id="shop_retrieve">
    <Core:PCProperties>
      <Core:Property property="merry:global:action">
         X[M] /* shop action - gives syntax and current info*/
if ((\$evoke)\&\&(\$evoke[1]\>48)\&\&(\$evoke[1]\<58))\{
  \$evoke=replace_strings(\$evoke,"\\"","");
  \$evoke=Int(\$evoke);
  \$sellarray = \$actor."sell:array";
  if (sizeof(\$sellarray)\<\$evoke+1)\{
    EmitTo(\$actor,"You can't retrieve an item that you didn't sell");
    return FALSE;
  \}
  \$sellitem = \$sellarray[\$evoke];
  \$loc=\$actor."base:environment";
  if (\$sellitem.item_home!=\$loc."base:objectname")\{
    \$itemhome=\$sellitem.item_home;
    \$itemhome=find_object(\$itemhome);
    EmitTo(\$actor,"That item is not here, you should look for it at "+Describe(\$itemhome));
    return FALSE;
  \}
  \$seller=\$sellitem."sell:seller";
  if (\$seller!=\$actor)\{
    EmitTo(\$actor,"You can no longer retrieve that item, it has been sold");
    return FALSE;
  \}
  /* CODE FOR SUCCESSFUL COMPLETION */
  \$sellitem."sell:run" = \$sellitem."sell:seller";
  \$sellitem."sell:seller" = nil;
  /* CODE FOR price LINKED LIST */
  \$priceheadname=\$sellitem."price:head";
  \$prevob=\$sellitem."price:prev";
  \$nextob=\$sellitem."price:next";
  if (\$prevob) \{ \$prevob."price:next" = \$nextob; \}
  else \{ Set(\$loc,\$priceheadname,\$nextob); \}
  if (\$nextob) \{ \$nextob."price:prev" = \$prevob; \}
  \$sellitem."price:prev" = nil;
  \$sellitem."price:next" = nil;
  /* CODE FOR sell LINKED LIST */
  \$prevob=\$sellitem."sell:prev";
  \$nextob=\$sellitem."sell:next";
  if (\$prevob) \{ \$prevob."sell:next" = \$nextob; \}
  else \{ \$actor."sell:head" = \$nextob; \}
  if (\$nextob) \{ \$nextob."sell:prev" = \$prevob; \}
  \$sellitem."sell:prev" = nil;
  \$sellitem."sell:next" = nil;
  /* CODE FOR EMIT AFTER SUCCESSFUL LIST EFFECTS */
  if (\$sellitem."sell:stack")\{
    Call(nil /* defunct */,"buyq",\$buyq_num: \$sellitem."sell:stack"-1, \$buyq_item: \$sellitem);
    EmitTo(\$actor,"You have retrieved "+Describe(\$sellitem)+" (stack of "+\$sellitem."sell:stack"+") from the market");
  \}
  else\{
    EmitTo(\$actor,"You have retrieved "+Describe(\$sellitem)+" from the market");
  \}
  \$sellitem."base:environment" = \$actor;
  \$sellitem."sell:run" = nil;
  return FALSE;
\}
EmitTo(\$actor,"Syntax: retrieve ['listnum] ( \<-- remember the quote mark! )");
\$list=\$actor."sell:head";
\$i=1;
\$sellarray=(\{\$list\});
if (!\$list)\{
  EmitTo(\$actor,"You have no items for sell in the market");
\}
while (\$list)\{
  \$qname=Call(nil /* defunct */,"qname",\$qname_num: \$list.quality);
  if ((\$list."sell:stack")\&\&(\$list."sell:stack"\>1))\{
    EmitTo(\$actor,"---\> "+\$i+" : "+Describe(\$list)+" ("+\$qname+") : "+\$list."sell:price"+" suns each (stack of "+\$list."sell:stack"+") ");
  \}
  else\{
    EmitTo(\$actor,"---\> "+\$i+" : "+Describe(\$list)+" ("+\$qname+") : "+\$list."sell:price"+" suns");
  \}
  \$sellarray+=(\{\$list\});
  \$i++;
  \$list=\$list."sell:next";
\}
\$actor."sell:array" = \$sellarray;
return FALSE;
      </Core:Property>
      <Core:Property property="revisions">
         (\{ 1186174365, "-", "SYNC" \})
      </Core:Property>
    </Core:PCProperties>
  </SkotOS:MerAcHa>
</object>
